// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Memory.jack

/**
 * This library provides two services: direct access to the computer's main
 * memory (RAM), and allocation and recycling of memory blocks. The Hack RAM
 * consists of 32,768 words, each holding a 16-bit binary number.
 */ 
class Memory {

    static Array memory;
    static int firstPointer;

    /** Initializes the class. */
    function void init() {
        let memory = 0;
        let firstPointer = 2048;
        let memory[2048] = 14336;
        let memory[2049] = 0;
        return;
    }

    /** Returns the RAM value at the given address. */
    function int peek(int address) {
        return memory[address];
    }

    /** Sets the RAM value at the given address to the given value. */
    function void poke(int address, int value) {
        let memory[address] = value;
        return;
    }

    /** Finds an available RAM block of the given size and returns
     *  a reference to its base address. */
    function int alloc(int size) {
        var int baseAdd, next;
        let next = firstPointer;
        while (memory[next] < size + 3) {
            let next = memory[next + 1];
        }
        let memory[next] = memory[next] - size -2;
        let baseAdd = memory[next] + next;
        let memory[baseAdd] = size + 2;
        return (baseAdd + 2);
    }

    /** De-allocates the given object (cast as an array) by making
     *  it available for future allocations. */
    function void deAlloc(Array o) {
        let memory[o-1] = firstPointer;
        let firstPointer = o - 2;
        return;
    }    
}

// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl
/** 
 * The Memory chip implements the complete address space of the Hack data memory,
 * including RAM, memory mapped screen, and memory mapped keyboard.
 * Outputs the value of the memory location specified by the address input.
 * If (load == 1), sets the memory location specified by the address input
 * to the value of the in input.
 * Address space rules:
 * Only the upper 16K + 8K + 1 words of the memory are used.
 * Access to address 0 to 16383 (0x0000 to 0x3FFF) results in accessing the RAM;
 * Access to address 16384 to 24575 (0x4000 to 0x5FFF) results in accessing the Screen memory map;
 * Access to address 24576 (0x6000) results in accessing the Keyboard memory map.
 */ 
CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // address[14]=0 for ram register indices
    // address[14]=1 for screen and greater register indices
    DMux(in=load, sel=address[14], a=a, b=b);
    RAM16K(in=in, load=a, address=address[0..13], out=outram);
    Screen(in=in, load=b, address=address[0..12], out=outscreen);
    Keyboard(out=outkbd);
    Mux16(a=outram, b=outscreen, sel=address[14], out=outmux1);
    And(a=address[13], b=address[14], out=outand);
    Mux16(a=outmux1, b=outkbd, sel=outand, out=out);
}
